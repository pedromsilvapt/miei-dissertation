$script::player::add_sequencer( $__dir__ + '/marina.html' );

# Title: Soft to Be Strong
# Artist: Marina
V70 L1 T120;

fun accomp () {
    [^Cm];
    [BM]; 
    [AM]; 
    [BM];
    [^Cm];
    [AM];
    [BM]2;

    [^Cm];
    [BM];
    [AM];
    [BM];
    [^Cm];
    [AM];
    [BM]2;
};

fun melody () {
    V120;

    r/4 ^g/4 ^g/4 ^g/4;
    ^f/2 e/8 ^d3/8; 
    ^c2;

    r/4 ^g/4 b/4; 
    ^c'/2 ^c'/4 ^c'/4;
    b5/4; 
    b/8 e'/8 ^d'/8 e'/8 b/4;
    ^c'/4 ^g/4 ^g/4 ^g/4;
    ^f/2 e/8 ^d3/8;
    ^c2;

    r/4 ^g/4 b/4;
    ^c'/2 ^c'/4 ^c'/4;
};

$toggle_sustain = fun ( ref $enabled ) => {
    if ( $enabled ) {
        cc( 64; 0 );
    } else {
        cc( 64; 127 );
    };

    $enabled = not $enabled;
};

fun keyboard () {
    $sustained = true;

    @keyboard { q toggle: accomp() | melody(); };
    
    @keyboard hold extend {
        a: [^Cm];
        s: [BM];
        d: [AM];
        f: [EM];
        g: [^Fm];
    };
    
    @keyboard hold extend (V120) {
        1: ^c;
        2: ^d; 
        3: e;
        4: ^f;
        5: ^g;
        6: b;
        7: ^c';
        8: ^d';
        9: e';

        c: toggle_sustain( $sustained );
    };
    
    @keyboard {
        "ctrl+\\": { keyboard\repl(); keyboard\on_release( "ctrl+\\" ) };
        [ keyboard_mido\MidiEvent() ] ( $message ): debug( $message );
        # [ keyboard\MouseMove() ] ( $x, $y ): debug( $x + $y );
    };
};

keyboard();

# We can also record our performance
# keyboard\record( $__dir__ + "/marina-perf-2.csv" );
# And then replay it
# keyboard\replay( $__dir__ + "/marina-perf-2.csv" );
# Or read the performance as a music sequence
# $music = keyboard\readperf( $__dir__ + "/marina-perf-2.csv" );

# print( to_mkl( keyboard\readperf( $__dir__ + "/marina-perf-2.csv" ) ) );

#(
          #[^CF^G]1/16 [A^ce]1/9
        #| r6/31 ^g1/18 r1/6 ^g1/14 r3/17 ^g1/15 r1/5 ^f1/15 r21/29 e1/17 r2/17 ^d2/29 r6/25 ^c371201/2484720 ^c180959/2484720
#)