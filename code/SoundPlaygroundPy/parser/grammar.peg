body <- statement ( ";" statement )* ";"? _;

statement <- _ ( var_declaration / instrument_declaration / expression ) _;

var_declaration <- "$" alphanumeric _ "=" _ expression;

instrument_declaration <- ":" alphanumeric _ "=" _ integer;

expression <- value_expression / music_expression;

music_expression <- sequence ( _ "|" _ sequence )*;

sequence <- repeat ( _ repeat)*;

repeat
    <- expression_unambiguous _ "*" _ integer
     / expression_unambiguous
     ;

expression_unambiguous
    <- variable / function / group / chord / note / rest / modifier / instrument_modifier;

group <- "(" _ music_expression _ ")";

note <- note_pitch ( _ note_value )?;

variable <- "$" alphanumeric;

function <- alphanumeric "(" _ function_parameters? _ ")";

function_parameters <- expression ( ";" expression )*;

chord <- "[" _ note ( _ note )* _ "]";

rest <- "r" ( _ note_value )?;

note_value
    <- "/" _ integer
     / integer _ "/" _ integer
     / integer
     ;

note_pitch
    <- r"[cdefgab]" "'"*
     / r"[CDEFGAB]" ","*
     ;

modifier
    <- r"[tT]" _ integer
     / r"[vV]" _ integer
     / r"[lL]" _ note_value
     / r"[sS]" _ integer _ "/" _ integer
     / r"[sS]" _ integer
     / r"[oO]" _ integer
     ;

instrument_modifier <- ":" alphanumeric _ sequence;

value_expression <- string_value / number_value;

string_value <- double_string / single_string;

double_string <- "\"" double_string_char* "\"";

double_string_char 
    <- "\\\""
     / "\\\\"
     / r"[^\"]"
     ;

single_string <- "'" single_string_char* "'";

single_string_char
    <- "\\'"
     / "\\\\"
     / r"[^']"
     ;

number_value <- float / integer;

float <- r"[0-9]+\.[0-9]+";

integer <- r"[0-9]+";

alphanumeric <- r"[a-zA-Z][a-zA-Z0-9]*";

_ <- r"[ \t\r\n]*";
